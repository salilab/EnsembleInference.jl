# irreducible unitary representations of the groups SO(3) and SE(3) and representations of
# their Lie algebras.
# For SO(3), see
# Varshalovich, D. A., Moskalev, A. N. & Khersonskii, V. K. Quantum Theory of Angular
# Momentum. 1988. World Scientific, Singapore. ISBN: 9971-5-0107-4.
# For SO(3) and SE(3), see ¬ß12.9 and 12.12 of
# Chirikjian, G. S. Stochastic Models, Information Theory, and Lie Groups, Volume 2. 2012.
# ISBN: 978-0-8176-4943-2. doi: 10.1007/978-0-8176-4944-9.

# diagonals of infinitesimal rotation elements of Varshalovich ¬ß4.18.3.
function representation_diag(::typeof(so3), ::typeof(E1), ‚Ñì, ::Val{i}) where {i}
    ‚Ñì2 = Int(2‚Ñì)
    absi = abs(i)
    isone(absi) && return @. im * (-sqrt((‚Ñì2:-1:1) * (1:‚Ñì2)) / 2)
    return zeros(complex(float(eltype(‚Ñì))), ‚Ñì2 + 1 - absi)
end
function representation_diag(::typeof(so3), ::typeof(E2), ‚Ñì, ::Val{i}) where {i}
    ‚Ñì2 = Int(2‚Ñì)
    absi = abs(i)
    isone(absi) && return @. i * sqrt((‚Ñì2:-1:1) * (1:‚Ñì2)) / 2
    return zeros(float(eltype(‚Ñì)), ‚Ñì2 + 1 - absi)
end
function representation_diag(::typeof(so3), ::typeof(E3), ‚Ñì, ::Val{i}) where {i}
    iszero(i) && return Vector(im .* (‚Ñì:-1:(-‚Ñì)))
    return zeros(complex(eltype(‚Ñì)), Int(2‚Ñì) + 1 - abs(i))
end

"""
    representation_block(::typeof(so3), X, ‚Ñì)

Compute the size `(2‚Ñì+1, 2‚Ñì+1)` block ``‚Ñì`` of the representation of the element ``X`` of
the element of the Lie algebra ``ùî∞ùî¨(3)``.

The representation is defined as

```math
u^‚Ñì(X) = \\lim_{t ‚Üí 0} \\frac{d}{dt} U^‚Ñì(\\exp(t X)),
```

where ``U^‚Ñì(R)`` is the representation of the rotation ``R``, and ``\\exp(R)`` is the group
exponential map on ``\\mathrm{SO}(3)``.
"""
representation_block(::typeof(so3), X, ‚Ñì)

function representation_block(::typeof(so3), Ei::BasisVector, ‚Ñì)
    dl = representation_diag(so3, Ei, ‚Ñì, Val(-1))
    d = representation_diag(so3, Ei, ‚Ñì, Val(0))
    du = representation_diag(so3, Ei, ‚Ñì, Val(1))
    return Tridiagonal(dl, d, du)
end
function representation_block(::typeof(so3), ::typeof(E3), ‚Ñì)
    return Diagonal(representation_diag(so3, E3, ‚Ñì, Val(0)))
end
function representation_block(::typeof(so3), X‚Å±::AbstractVector{<:Real}, ‚Ñì)
    # because representation_element returns a result with real eltype Float64, we use `T`
    # to avoid promoting if the eltype of X‚Å± is lower precision
    T = complex(float(eltype(X‚Å±)))
    uE1 = representation_block(so3, E1, ‚Ñì)
    uE2 = representation_block(so3, E2, ‚Ñì)
    uE3 = representation_block(so3, E3, ‚Ñì)
    return Tridiagonal(
        X‚Å±[1] .* T.(uE1.dl) .+ X‚Å±[2] .* T.(uE2.dl),
        X‚Å±[3] .* T.(uE3.diag),
        X‚Å±[1] .* T.(uE1.du) .+ X‚Å±[2] .* T.(uE2.du),
    )
end
function representation_block(::typeof(so3), X::AbstractMatrix, ‚Ñì)
    # represent the vector X as an actual Vector using the basis
    X‚Å± = Manifolds.vee(SO3, Manifolds.Identity(SO3), X)
    return representation_block(so3, X‚Å±, ‚Ñì)
end

"""
    representation_block(::typeof(SO3), p, ‚Ñì) -> Matrix{Complex}

Compute the size `(2‚Ñì+1, 2‚Ñì+1)` block ``‚Ñì`` of the representation of the rotation ``p``.

This block is identical to the ``D^‚Ñì`` block of the Wigner ``D``-matrix using the
conventions of [^Varshalovich1988].

[^Varshalovich1988]: Varshalovich, D. A., Moskalev, A. N. & Khersonskii, V. K. Quantum Theory of Angular
    Momentum. 1988. World Scientific, Singapore. ISBN: 9971-5-0107-4.
# Examples

To create the "little-``d``" block ``d^‚Ñì(\\frac{œÄ}{4}) = D^‚Ñì(0, \\frac{œÄ}{4}, 0)``,

```julia-repl
julia> using EnsembleInference, Rotations

julia> EnsembleInference.representation_block(SO3, RotZXZ(0, œÄ/2, 0), 1)
3√ó3 Matrix{ComplexF64}:
  0.5+0.0im              0.0-0.707107im  -0.5+0.0im
  0.0-0.707107im  1.2175e-16+0.0im        0.0-0.707107im
 -0.5+0.0im              0.0-0.707107im   0.5+0.0im

julia> EnsembleInference.representation_block(SO3, RotZXZ(0, œÄ/2, 0), 2)
5√ó5 Matrix{ComplexF64}:
      0.25+0.0im   0.0-0.5im          -0.612372+0.0im           0.0+0.5im               0.25+0.0im
       0.0-0.5im  -0.5-0.0im                0.0-3.24998e-16im  -0.5-0.0im                0.0+0.5im
 -0.612372-0.0im   0.0-2.41689e-16im       -0.5-0.0im           0.0-3.62534e-16im  -0.612372-0.0im
       0.0+0.5im  -0.5+0.0im                0.0-2.03875e-16im  -0.5+0.0im                0.0-0.5im
      0.25+0.0im   0.0+0.5im          -0.612372+0.0im           0.0-0.5im               0.25+0.0im
```
"""
function representation_block(::typeof(SO3), p, ‚Ñì)
    X = Manifolds.group_log(SO3, p)
    u = representation_block(so3, X, ‚Ñì)
    return exp(Matrix(u))
end
